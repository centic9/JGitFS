apply plugin: 'java'
//apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin:'application'
apply plugin:'jacoco'

sourceCompatibility = 1.8

mainClassName = "org.dstadler.jgitfs.JGitFS"

repositories {
  mavenCentral()
}

dependencies {
	compile 'org.dstadler:commons-dost:1.0.0.+'
	compile 'commons-io:commons-io:2.5'
	compile 'org.apache.commons:commons-lang3:3.4'
	compile 'net.java.dev.jna:jna:3.5.2'
	compile 'org.eclipse.jgit:org.eclipse.jgit:4.5.0.201609210915-r'
	compile 'com.google.guava:guava:19.0'
	compile project(':fuse-jna')
	compile 'jline:jline:2.14.2'

	runtime 'org.slf4j:slf4j-simple:1.7.21'

	testCompile 'junit:junit:4.12'
	testCompile 'org.dstadler:commons-test:1.0.0.+'
}

// work around unnecessary timestamp in generated file which always causes dirty files in version control
// https://issues.gradle.org/browse/GRADLE-2293
task adjustEclipseSettingsFile << {
	ant.replaceregexp(match:'^#.*', replace:'', flags:'g', byline:true) {
		fileset(dir: project.projectDir, includes: '.settings/org.eclipse.jdt.core.prefs,.settings/com.google.gdt.eclipse.core.prefs')
	}
}
task sortEclipseSettingsFile << {
	new File(project.projectDir, '.settings/org.eclipse.jdt.core.prefs').with { it.text = it.readLines().findAll { it }.sort().join('\n') }
	new File(project.projectDir, '.classpath').with { it.text = it.readLines().findAll { it }.unique().join('\n') }
	File file = new File(project.projectDir, '.settings/com.google.gdt.eclipse.core.prefs');
	if(file.exists()) {
		file.with { it.text = it.readLines().findAll { it }.sort().join('\n') }
	}
	ant.fixcrlf(srcDir: '.settings', eol: 'lf')
}
eclipseJdt.finalizedBy adjustEclipseSettingsFile
eclipseJdt.finalizedBy sortEclipseSettingsFile

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

task adjustWrapperPropertiesFile << {
	ant.replaceregexp(match:'^#.*', replace:'', flags:'g', byline:true) {
		fileset(dir: project.projectDir, includes: 'gradle/wrapper/gradle-wrapper.properties')
	}
	new File(project.projectDir, 'gradle/wrapper/gradle-wrapper.properties').with { it.text = it.readLines().findAll { it }.sort().join('\n') }
	ant.fixcrlf(file: 'gradle/wrapper/gradle-wrapper.properties', eol: 'lf')
}
wrapper.finalizedBy adjustWrapperPropertiesFile

tasks.withType(JavaCompile){
	// avoid: :compileJavawarning: [options] bootstrap class path not set in conjunction with -source 1.7
	options.warnings = false
	options.compilerArgs = [ '-Xlint:deprecation' ]
    options.encoding = 'UTF-8'
}

jacoco {
	toolVersion = '0.7.7.201606060606'
}

jacocoTestReport {
    reports {
        xml.enabled true
	}
}
