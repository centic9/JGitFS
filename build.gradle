apply plugin: 'java'
apply plugin:'application'
apply plugin:'jacoco'

sourceCompatibility = 1.8

mainClassName = "org.dstadler.jgitfs.JGitFS"

repositories {
  mavenCentral()
}

dependencies {
	compile 'org.dstadler:commons-dost:1.0.0.30'
	compile 'commons-io:commons-io:2.6'
	compile 'org.apache.commons:commons-lang3:3.10'
	compile 'net.java.dev.jna:jna:3.5.2'
	compile 'org.eclipse.jgit:org.eclipse.jgit:5.6.1.202002131546-r'
	compile 'com.google.guava:guava:28.2-jre'
	compile files('lib/fuse-jna-1.0.3.1.jar')
	compile 'jline:jline:2.14.6'

	runtime 'org.slf4j:slf4j-simple:1.7.30'

	testCompile 'junit:junit:4.13'
	testCompile 'org.dstadler:commons-test:1.0.0.18'
}

wrapper {
    gradleVersion = '5.6.4'
}

task adjustWrapperPropertiesFile {
    doLast {
        ant.replaceregexp(match: '^#.*', replace: '', flags: 'g', byline: true) {
            fileset(dir: project.projectDir, includes: 'gradle/wrapper/gradle-wrapper.properties')
        }
        new File(project.projectDir, 'gradle/wrapper/gradle-wrapper.properties').with {
            it.text = it.readLines().findAll { it }.sort().join('\n')
        }
        ant.fixcrlf(file: 'gradle/wrapper/gradle-wrapper.properties', eol: 'lf')
    }
}
wrapper.finalizedBy adjustWrapperPropertiesFile

tasks.withType(JavaCompile){
	// avoid: :compileJavawarning: [options] bootstrap class path not set in conjunction with -source 1.7
	options.warnings = false
	options.compilerArgs = [ '-Xlint:deprecation' ]
    options.encoding = 'UTF-8'
}

jacoco {
	toolVersion = '0.8.5'
}

jacocoTestReport {
    reports {
        xml.enabled true
	}
}
