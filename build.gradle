apply plugin: 'java'
apply plugin:'application'
apply plugin:'jacoco'

mainClassName = "org.dstadler.jgitfs.JGitFS"

repositories {
  mavenCentral()
}

dependencies {
    implementation 'commons-cli:commons-cli:1.5.0'
	implementation 'org.dstadler:commons-dost:1.2.0.2'
	implementation 'commons-io:commons-io:2.13.0'
	implementation 'org.apache.commons:commons-lang3:3.13.0'
	implementation 'net.java.dev.jna:jna:3.5.2'
	implementation 'org.eclipse.jgit:org.eclipse.jgit:6.7.0.202309050840-r'
	implementation 'com.google.guava:guava:32.1.2-jre'
	implementation files('lib/fuse-jna-1.0.3.2.jar')
	implementation 'jline:jline:2.14.6'

	runtimeOnly 'org.slf4j:slf4j-simple:1.7.36'

	testImplementation 'junit:junit:4.13.2'
	testImplementation 'org.dstadler:commons-test:1.0.0.20'
}

wrapper {
    gradleVersion = '7.6.1'
}

tasks.register('adjustWrapperPropertiesFile') {
	doLast {
		ant.replaceregexp(match: '^#.*', replace: '', flags: 'g', byline: true) {
			fileset(dir: project.projectDir, includes: 'gradle/wrapper/gradle-wrapper.properties')
		}
		new File(project.projectDir, 'gradle/wrapper/gradle-wrapper.properties').with {
			it.text = it.readLines().findAll { it }.sort().join('\n')
		}
		ant.fixcrlf(file: 'gradle/wrapper/gradle-wrapper.properties', eol: 'lf')
	}
}
wrapper.finalizedBy adjustWrapperPropertiesFile

tasks.withType(JavaCompile).configureEach {
	// avoid: :compileJavawarning: [options] bootstrap class path not set in conjunction with -source 1.7
	options.warnings = false
	options.compilerArgs = ['-Xlint:deprecation']
	options.encoding = 'UTF-8'
}

jacoco {
	toolVersion = '0.8.10'
}

jacocoTestReport {
    reports {
        xml.enabled true
	}
}
